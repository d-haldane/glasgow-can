#use-added-syntax(esir)
defpackage design-generator :
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import esir/repl-lib
  import jitpcb/visualizer
  import ocdb/tests/default-harness
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/bundles
  import glasgow
  ; import ocdb/db-parts

; Main module. Code inside here gets converted to hardware.
pcb-module can-addon :
  ; Plan for vio-a = vio-b = 5.0
  inst glasgow : {glasgow/add-on(5.0, 5.0)}
  net gnd (glasgow.gnd)

  inst xr-a : {ocdb/texas-instruments/TCAN1051/module}
  inst j-a : {ocdb/amphenol/delta-d/component(9, "male", "vertical")}
  net (xr-a.gnd, j-a.p[2] j-a.shield, gnd)
  net CANA-H (xr-a.can.canh j-a.p[3])
  net CANA-L (xr-a.can.canl j-a.p[5])

  net CANA-TX (xr-a.uart.tx, glasgow.A.io[1])
  net CANA-RX (xr-a.uart.rx, glasgow.A.io[2])
  net CANA-S  (xr-a.s,       glasgow.A.io[3])
  net VIO-A   (xr-a.vcc, xr-a.vio,  glasgow.A.vio.vdd)
  ocdb/protection/esd-clamp(xr-a.can, gnd)
  ocdb/terminations/terminate-can(xr-a.can, gnd, "isolated-switchable", glasgow.A.io[4])

  ; Isolated channel.
  inst xr-b : {ocdb/texas-instruments/TCAN1051/module}
  inst j-b : {ocdb/amphenol/delta-d/component(9, "female", "right-angle")}

  net VIO-B  (glasgow.B.vio.vdd)
  val isolated-viob = ocdb/protection/isolate-power(glasgow.B.vio)

  net (xr-b.gnd, j-b.p[2] j-b.shield, {isolated-viob}.gnd)
  net (xr-b.can.canh j-b.p[3])
  net (xr-b.can.canl j-b.p[5])
  ocdb/protection/isolate-logic([[glasgow.B.io[1] xr-b.uart.tx]
                                 [glasgow.B.io[2] xr-b.uart.rx]
                                 [glasgow.B.io[3] xr-b.s]], 
                                  glasgow.B.vio,  isolated-viob)

  net VIO-B-ISO (xr-b.vcc, xr-b.vio,  {isolated-viob}.vdd)
  net ISO-GND ({isolated-viob}.gnd)

  ocdb/protection/esd-clamp(xr-b.can, isolated-viob.gnd)
  ocdb/terminations/terminate-can(xr-b.can, gnd, "isolated-switchable", glasgow.B.io[4])

  package(glasgow) at loc(0.0,0.0) on Top
  group-modules([[glasgow] [xr-b j-b] [xr-a j-a]])
  symbol(gnd) = {ocdb/symbols/ground-sym}
  symbol(VIO-B-ISO) = {ocdb/symbols/supply-sym}
  symbol(VIO-A) = {ocdb/symbols/supply-sym}
  symbol(VIO-B) = {ocdb/symbols/supply-sym}

val main-design = default-board(can-addon, 2, 48.0, 40.0)
view(main-design)

export-kicad("can-add-on", [`place => true 
                      `gen-board => true 
                      `gen-schematic => true 
                      `check-log => "can.txt" 
                      `fresh => true 
                      `schematic-version => 4
                      `gen-bom => true 
                      `param-configs => [`groups]] )

