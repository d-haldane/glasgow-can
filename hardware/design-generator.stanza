#use-added-syntax(esir)
defpackage design-generator :
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import esir/repl-lib
  import jitpcb/visualizer
  import ocdb/tests/default-harness
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/bundles
  import glasgow

; Main module. Code inside here gets converted to hardware.
pcb-module can-addon :
  ; Plan for vio-a = vio-b = 5.0
  inst glasgow : {glasgow/add-on(5.0, 5.0)}
  net gnd (glasgow.gnd)

  inst xr-a : {ocdb/texas-instruments/TCAN1051/module}
  inst j-a : {ocdb/amphenol/delta-d/component(9, "female", "right-angle")}
  net (xr-a.gnd, j-a.p[2] j-a.shield, gnd)
  net (xr-a.can.canh j-a.p[3])
  net (xr-a.can.canl j-a.p[5])
  require can-a:{banked-io("A")}[4] from glasgow
  net (xr-a.uart.tx, can-a[0].gpio)
  net (xr-a.uart.rx, can-a[1].gpio)
  net (xr-a.s,       can-a[2].gpio)
  net (xr-a.vcc, xr-a.vio,  glasgow.A.vio)
  ocdb/protection/esd-clamp(xr-a.can, gnd)
  ocdb/terminations/terminate-can(xr-a.can, gnd, "isolated-switchable", can-a[3].gpio)

  inst xr-b : {ocdb/texas-instruments/TCAN1051/module}
  inst j-b : {ocdb/amphenol/delta-d/component(9, "female", "right-angle")}
  net (xr-b.gnd, j-b.p[2] j-b.shield, gnd)
  require can-b:{banked-io("B")}[4] from glasgow
  net (xr-b.can.canh j-b.p[3])
  net (xr-b.can.canl j-b.p[5])
  net (xr-b.uart.tx, can-b[0].gpio)
  net (xr-b.uart.rx, can-b[1].gpio)
  net (xr-b.s,       can-b[2].gpio)
  net (xr-b.vcc, xr-b.vio,  glasgow.B.vio)
  ocdb/protection/esd-clamp(xr-b.can, gnd)
  ocdb/terminations/terminate-can(xr-b.can, gnd, "isolated-switchable", can-b[3].gpio)

  package(glasgow) at loc(0.0,0.0) on Top
  layout-group(xr-a) = A
  schematic-group(xr-a) = A
  layout-group(xr-b) = B
  schematic-group(xr-b) = B

print-esir("Input.txt")

val main-design = default-board(can-addon, 2, 48.0, 40.0)
view(main-design)

export-kicad("can-add-on", [`place => true 
                      `gen-board => true 
                      `gen-schematic => true 
                      `fresh => true 
                      `schematic-version => 4
                      `param-configs => [`sketch]] )

                        ; `gen-bom => true 
